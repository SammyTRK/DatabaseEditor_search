apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("key.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    signingConfigs {
        gearSigningConfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 24
    buildToolsVersion "23.0.3"
    defaultConfig {
        applicationId "com.wubydax.dbeditor"
        minSdkVersion 23
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        project.archivesBaseName = "DBEditor"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.gearSigningConfig
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFile = new File(output.outputFile.parent,
                            output.outputFile.name.replace("-release", ""))
                }
            }
        }

    }
}

task pushToDevice(type: Exec, dependsOn: 'assembleRelease') {
    commandLine 'adb', 'push', './build/outputs/apk/DBEditor.apk', '/sdcard/DBEditor.apk'
}

task chmodApk(type: Exec) {
    commandLine 'adb', 'shell', 'su', '-c', 'chmod 644 /system/priv-app/DBEditor.apk'
    finalizedBy('mountRO')
}

task copyToPrivApp(type: Exec, dependsOn: 'mountRW') {
    commandLine 'adb', 'shell', 'su', '-c', 'cp /storage/emulated/0/DBEditor.apk /system/priv-app/DBEditor.apk'
    finalizedBy('chmodApk')
}

task mountRW(type: Exec, dependsOn: 'pushToDevice') {
    commandLine 'adb', 'shell', 'su', '-c', 'mount -o remount,rw /system'
}

task mountRO(type: Exec) {
    commandLine 'adb', 'shell', 'su', '-c', 'mount -o remount,ro /system'
    finalizedBy('rebootDevice')
}

task rebootDevice(type: Exec) {
    commandLine 'adb', 'reboot'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:design:24.1.1'
    compile 'com.android.support:cardview-v7:24.1.1'
}
